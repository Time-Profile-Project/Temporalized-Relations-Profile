# Title: Missing rdfs:label
# Description: Any resource must have an rdfs:label, with the following exceptions - deprecated resources.
# Severity: Warning

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?resource ?property ?value 
WHERE {
  VALUES ?property { rdfs:label }
  ?resource ?p ?o .
  FILTER NOT EXISTS { ?resource ?property ?value .
                      FILTER(str(?value) != "") }
  FILTER NOT EXISTS { ?resource a owl:Ontology }
  FILTER NOT EXISTS { ?resource owl:deprecated true }
  FILTER EXISTS { ?resource ?p2 ?o2 .
    FILTER (?p2 != rdf:type)
    FILTER (?p2 != owl:equivalentClass)
    FILTER (?p2 != owl:disjointWith)
    FILTER (?p2 != owl:equivalentProperty)
    FILTER (?p2 != owl:inverseOf)
  }
  FILTER (!isBlank(?resource))
} ORDER BY ?resource
